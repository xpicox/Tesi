%!TEX root = thesis.tex

\chapter{Open Memory Transactions in Haskell}

Haskell was chosen as the development language because of its very expressive type system that offers a perfect environment for studying the ideas of transactional memory.
In \cite{Harris:2005:CMT:1065944.1065952} this has been used to single out computations which may be used in transactions, from those which can perform irreversible I/O effects. 
In the implementation, this idea is further improved by using the type system to separate \emph{isolated} transactions from those who can interact, and hence be merged.

\begin{figure}
    \centering
    \begin{Verbatim}[tabsize=3, gobble=2]
        data ITM a
        data OTM a
        -- henceforth, t is a placeholder for ITM or OTM --
        
        -- Sequencing, do notation ------------------------
        (>>=)  :: t a -> (a -> t b) -> t b
        return :: a -> t a
        
        -- Running isolated and atomic computations -------
        atomic   :: OTM a -> IO a
        isolated :: ITM a -> OTM a
        
        -- Composing transactions -------------------------
        class (Monad m) => MonadTransaction m where
            retry    :: m a
            orElse   :: m a -> m a -> m a
        
        -- Exceptions -------------------------------------
        throw :: Exception e => e -> t a
        catch :: Exception e => t a -> (e -> t a) -> t a
        
        -- Threading --------------------------------------
        fork :: OTM () -> OTM ThreadId
        
        -- Transactional memory ---------------------------
        data OTVar a
        class (Monad m) => MonadTM m where
            newOTVar     :: a -> m (OTVar a)
            readOTVar    :: OTVar a -> m a
            writeOTVar   :: OTVar a -> a -> m ()

    \end{Verbatim}
    \caption{The base interface of OTM.}
    \label{fig:base-interface}
\end{figure}

\section{Transactional actions and variables}
The key idea introduced by \citet{OpenTransactionsSpec} is to separate isolation from atomicity: isolation is a computational aspect that can be added to atomic transactions.
Following this perspective, at the type system level we distinguish isolated atomic actions, represented as values of type \emph{ITM a}, and non isolated atomic actions, as values of type \emph{OTM a}. Actions can be sequentially composed with the corresponding bind operator preserving atomicity and, for ITM actions, isolation.

The function \textcode{isolated} takes an isolated atomic action and delivers an atomic action whose effects are guaranteed to be executed in isolation with respect to other actions.
Then, \emph{atomic} takes an atomic action and delivers an I/O action that when performed runs a transaction whose effects are kept tentative until it commits.
Tentative effects are shared among all non isolated transactions. Values of type \emph{STM a} can be seen as values of type \emph{ITM a}: the I/O they deliver is the same, since \emph{atomically} can be expressed as:
\begin{Verbatim}
    atomically = atomic . isolated
\end{Verbatim}

Like \emph{STM}, \emph{OTM} provides a mechanism for safe thread
communication by means of transactional variables called \emph{OTVars}
which, differently from TVars, support \emph{open} transactions.
These are values of type \emph{OTVar a} where \emph{a} is the type of value held.
Creating, reading and writing \emph{OTVars} is done via the interface defined by the \emph{MonadTM} class shown in \cref{fig:base-interface}. 
All these actions are both atomic and isolated, thus, when it comes to actions of type \emph{ITM a}, \emph{OTVars} are basically \emph{TVars};
\eg \emph{modifyTVar} from \emph{STM} corresponds to:

\begin{Verbatim}
    modifyOTVar :: OTVar a -> (a -> a) -> ITM ()
    modifyOTVar var f = do
        x <- readOTVar var
        writeOTVar var (f x) 
\end{Verbatim}
From its type it is immediate to see that the update is both atomic and isolated. In fact, read and write operations are glued together by the \textgreater \textgreater= combinator, preserving both properties.

\section{Blocking}
\emph{OTM} supports composable blocking via the primitive \textcode{retry}, 
under \emph{STM} slogan ``a thread that has to be blocked because it has 
been scheduled too soon''. As for \emph{STM}, retrying a transactional action 
actually corresponds to block the threads on some condition. Both isolated and non-isolated transactions are instances of \emph{MonadTransaction} class described in \cref{fig:base-interface}.

Checks may be declared as follows:
\begin{Verbatim}[tabsize=3, xleftmargin=1ex, gobble=1]
    check :: Bool -> ITM ()
    check b = if b then return () else retry
\end{Verbatim}
and invariants on transactional variables can be easily checked by composing reads and checks as follow:
\begin{Verbatim}
    assertOTVar :: OTVar a -> (a -> Bool) -> ITM ()
    assertOTVar var p = do
        x <- readOTVar var
        check (p x)
\end{Verbatim}

Synchronization primitives, such as semaphores, can be easily implemented with isolated actions.
A semaphore is a counter with two fundamental operations: \emph{up} which increments the counter and \emph{down} which decrements the counter if it is not zero and blocks otherwise.
Semaphores are implemented using \emph{OTM} as \emph{OTVars} holding a counter:
\begin{Verbatim}[tabsize=3, xleftmargin=1ex, gobble=1]
    type Semaphore = OTVar Int
\end{Verbatim}
Then, \textcode{up} and \textcode{down} are even more immediate than their description:
\begin{Verbatim}
    up :: Semaphore -> ITM ()
    up var = modifyOTvar var (1+)

    down :: Semaphore -> ITM ()
    down var = do
        assertOTVar var (> 0)
        modifyOTVar var (-1+)
\end{Verbatim}
Both are atomic and isolated updates with the latter being guarded by a pre-condition.

Actions can also be composed as \emph{alternatives} by means of the primitive \emph{orElse}. For instance, the following takes a family of semaphores and delivers an action that decrements one of them, and blocking only if none can be decremented:
\begin{Verbatim}
    downAny :: [Semaphore] -> ITM ()
    downAny (x:xs) = down x `orElse` downAny xs
    downAny [] = retry
\end{Verbatim}

\section{Thread interaction}

The interchangeability of \emph{OTM} and \emph{STM} ends when isolation is dropped.
\emph{OTM} offers shared \emph{OTVars} as a mechanism for safe \emph{transaction interaction}.
This means that non-isolated transactional actions see the effects on shared variables of any other non-isolated transactional action, as they are performed concurrently on the same object.
This introduces dependencies between concurrent tentative actions: an action can not make its effects permanent, if it depends on information produced by another action which fails to complete.
\emph{OTM} guarantees coherence of transactional actions in presence of interaction through shared transactional variables.

%Among several ways for threads to communicate and coordinate, OTVars enables loosely-coupled interaction right inside atomic actions taking the programming style of \emph{STM} a step further. 
A synchronization scenario that highlights how \emph{OTM} can take the programming style of \emph{STM} a step further is described below.

In this example a master process tries to outsource part of an atomic computation to some thread chosen from a worker pool; communication is coordinated by a pair of semaphores.
This can be achieved straightforwardly using \emph{OTM}:\\


\begin{minipage}{0.45\textwidth}
\begin{Verbatim}
        master c1 c2 = do
            -- put request
            isolated (up c1)
            -- do something else
            isolated (down c2)
            -- get answer
\end{Verbatim}
\end{minipage}
\begin{minipage}{0.45\textwidth}
\begin{Verbatim}
        worker c1 c2 = do
            -- do something
            isolated (down c1)
            -- get request
            -- put answer
            isolated (up c2)
\end{Verbatim}
\end{minipage}
\\

Both functions deliver atomic actions that synchronize using
\textcode{c1} and \textcode{c2}. 

\section{Concurrency}

Differently from \emph{STM}, \emph{OTM} supports parallelism inside non-isolated transactions.
We can easily fork new threads without leaving \emph{OTM} but, like any effect of a transactional action, thread creation and execution remain tentative until the whole transaction commits. 
Forked threads participate to their transaction and impact its life-cycle (e.g.~issuing aborts) as any other participant.
This means that before committing, all forked threads have to complete their transactional action, i.e.~terminate.
Therefore, although the whole effect delivered by the transaction has happened concurrently, forked threads  never leave a transaction alive.

% Because of their transactional nature, threads forked inside
% a transaction do not have compensations nor continuations 
% (i.e.~I/O actions to be executed after an abort or after a commit).
% Compensations are pointless since aborts revert all effects including 
% thread creation. It is indeed possible to replace the primitive 
% \textcode{fork} with one supporting I/O actions as continuations like
% \begin{Verbatim}[tabsize=3, xleftmargin=1ex, gobble=1]
%     forkCont :: OTM a -> (a -> IO ()) -> OTM ThreadID
% \end{Verbatim}
% In fact, this mechanism can be implemented with the primitives
% already offered \emph{OTM}:  since commits are synchronisation points,
% the above corresponds to the parent thread
% forking a thread for each continuation, after the atomic action is
% successfully completed.

On the other hand, by definition isolated atomic actions have to appear 
as being executed in a single-threaded setting; hence neither 
\emph{STM} nor \emph{ITM} actions support thread creation.



\section{Examples}
\paragraph{Crowdfunding}
In this example we consider a scenario in which
one party needs to atomically acquire a given number 
of resources which are offered by a dynamic group.
For the sake of exposition we rephrase the example
using the metaphor of a fundraiser's ``crowdfunding campaign'': 
the resources to be acquired are the campaign goal
and the resources are donated by a crowd of \emph{backers}.

\begin{figure}
    \centering
    \begin{Verbatim}[tabsize=3, gobble=2]
        type Account = OTVar Int
        type Campaign = (Account, Int)

        transfer :: Account -> Account -> Int -> ITM ()
        transfer a1 a2 n = do
            withdraw a1 n
            deposit a2 n

        newCampaign target = do
            a <- newOTvar 0
            return (a, target)
        
        backCampaign :: Account -> Campaign -> Int -> ITM ()
        backCampaign a (a',_) k = transfer a a' k
        
        commitCampaign :: Account -> Campaign -> ITM ()
        commitCampaign a (a', t) = do
            x <- readOTVar a'
            check (x >= t)
            transfer a' a x
    \end{Verbatim}
    \caption{Crowdfunding.}
    \label{fig:example-funding}
\end{figure}

Each participant has a bank account, i.e.~an OTVar holding an integer 
representing its balance. Accounts have two operation \textcode{deposit} 
and \textcode{withdraw} which are implemented on the lines of 
\textcode{up} and \textcode{down}, respectively.
A campaign have a temporary account to store funds
before transferring them to the fundraiser that closes
the campaign; this operation blocks until the goal 
is met. % (for simplicity we omitted time constraints).
Backer participants transfer a chosen amount of funds from their
account to the campaign account, but the transfer is delayed until the campaign is closed. Notice that participants do not need to know each other to coordinate.
The implementation is shown in Figure~\ref{fig:example-funding}.

\paragraph{Thread barriers}
Barriers are abstractions used to coordinate groups of threads;
once reached a barrier, threads cannot cross it
until all other participants reach the barrier. 
Thread groups can be either dynamic or static, depending
on whether threads may join % (and in some implementations leave)
the group or not. Here we consider dynamic groups. 

Threads interact with barriers through two base operations: \textcode{join} for  
joining the group associated with the barrier and \textcode{await} for blocking 
waiting all participants before crossing.

\begin{figure}
    \centering
    \begin{Verbatim}[tabsize=3, gobble=2]
        type Barrier = OTVar (Int, Int)

        newBarrier :: ITM Barrier
        newBarrier = newOTVar (0,0)    
    
        join :: Barrier -> ITM ()
        join b = do
            assertOTVar b nobodyWaiting
            modifyOTVar b (bimap (1+) id)
            
        await :: Barrier -> OTM ()
        await b = do
            isolated $ modifyOTVar b (bimap (-1+) (1+))
            isolated $ do
                assertOTVar b nobodyRunning
                modifyOTVar b (bimap id (-1+))
        
        nobodyRunning (r,_) = r == 0
        nobodyWaiting (_,w) = w == 0
        bimap f g (a, b) = (f a, g b)
    \end{Verbatim}
%    \begin{Verbatim}[tabsize=3, gobble=2]
%        type Barrier = OTVar (Int, Int)
%
%        newBarrier :: ITM Barrier
%        newBarrier = newOTVar (0,0)    
%    
%        join :: Barrier -> ITM ()
%        join b = do
%            assertOTVar b nobodyWaiting
%            modifyOTVar b recordJoin
%            
%        await :: Barrier -> OTM ()
%        await b = do
%            isolated $ modifyOTVar b recordAwait
%            isolated $ do
%                assertOTVar b nobodyRunning
%                modifyOTVar b recordLeave
%        
%        nobodyRunning (r,_) = r == 0
%        nobodyWaiting (_,w) = w == 0
%        recordJoin  (r,w) = (r+1,w)
%        recordAwait (r,w) = (r-1,w+1)
%        recordLeave (r,w) = (r,w-1)
%    \end{Verbatim}
%    \begin{Verbatim}[tabsize=3, gobble=2]
%        type Barrier = (OTVar Int, OTVar Int)
%
%        newBarrier :: ITM Barrier
%        newBarrier = do
%            running <- newOTVar 0
%            waiting <- newOTVar 0
%            return (running,waiting)
%            
%        join :: Barrier -> ITM ()
%        join (running,waiting) = do
%            assertOTVar waiting (== 0)
%            modifyOTVar running (1 +)
%            
%        await :: Barrier -> OTM ()
%        await b = do
%            isolated $ do
%                modifyOTVar running (-1 +)
%                modifyOTVar waiting (1 +)
%            isolated $ do
%                assertOTVar running (== 0)
%                modifyOTVar waiting (-1 +)
%    \end{Verbatim}
    \caption{Thread barrier.}
    \label{fig:example-barrier}
\end{figure}

Barriers can be implemented using \emph{OTM} in few lines as shown
in Figure~\ref{fig:example-barrier}. A barrier is composed by
a transactional variable holding a pair of counters 
tracking the number of participating threads that are waiting
or running. For the sake of simplicity, we prevent new joins 
during barrier crossing.
This is enforced by the assertion guarding the counter update
performed by \textcode{join}. 
Waiting and crossing correspond to the two isolated actions composing
\textcode{await}: the first changes the state of the thread from
running to waiting and the second ensures that all threads reached the barrier
before crossing and decrementing the waiting counter.
Differently from \textcode{join}, \textcode{await} cannot
be isolated: isolation would prevent other
participants from updating their state from ``running'' to ``waiting''.

This implementation is meant as a way to coordinate
concurrent transactional actions but it may be used
for coordinating concurrent I/O actions as it is.
The latter scenario could be implemented also using
\emph{STM} although \textcode{await} would necessarily
be an I/O action since it cannot be an
isolated atomic action (i.e., of type \emph{STM\;a}).

\paragraph{Atomic futures}
Suppose we want to delegate some task to another thread and 
then collect the result once it is ready. An intuitive way to 
achieve this is by means of \emph{futures}, i.e.~``proxy results'' 
that will be produced by the worker threads. 

\begin{figure}
    \centering
    \begin{Verbatim}[tabsize=3, gobble=2]
        type Future a = OTVar (Maybe a)
        
        spawn :: OTM a -> OTM (Future a)
        spawn job = do 
            future <- newOTVar Nothing
            fork (worker future)
            return future
            where
                worker :: Future a -> OTM ()
                worker future = do 
                    result <- job
                    writeOTVar future (Just $! result)
                    
        getFuture :: Future a -> ITM a
        getFuture f = 
            v <- readOTVar f
            case v of
                Nothing -> retry
                Just val -> return val        
    \end{Verbatim}
    \caption{Atomic futures.}%$
    \vspace{-1ex}
    \label{fig:example-futures}
\end{figure}

A future can be implemented in \emph{OTM} by a OTVar
holding a value of type \emph{Maybe a}: either it is
``not-ready-yet'' (\textcode{Nothing}) or it holds something 
of type \emph{a}.
Future values are retrieved via \textcode{getFuture} which takes 
a future and delivers an action that blocks until the value is
ready and then produces the value.
Futures are created by \textcode{spawn} which takes a transactional
action to be performed by a forked (transactional) thread. 
The full specification is in Figure~\ref{fig:example-futures}.

\paragraph{Petri nets}
Petri nets are a well-known (graphical) formal model for concurrent, discrete-event dynamic systems.
A Petri net is readily implemented in \emph{OTM} 
by representing each transition by a thread, and each place by a semaphore.
Each thread repeatedly simulates the firing of the transition it 
implements and updates its input and output places accordingly:
putting and taking a token from a place correspond to 
increasing (\textcode{up}) or decreasing (\textcode{down})
its semaphore---the latter blocks if no tokens are available.
These semaphore operations must be 
performed atomically but not in isolation; in fact, 
isolation would prevent transitions sharing a place to
fire concurrently. 
Using \emph{OTM}, all this is achieved in few lines as shown in \cref{fig:petrinets}.\newpage

\begin{figure}
\begin{Verbatim}[tabsize=3, xleftmargin=1ex, gobble=1]
    type Place = Semaphore
    
    transition :: [Place] -> [Place] -> IO ThreadId
    transition inputs outputs = forkIO (forever fire)
        where 
            fire = atomic $ do
                mapM_ (isolated . down) inputs
                mapM_ (isolated . up)   outputs
\end{Verbatim}
\caption{Petri nets}
\label{fig:petrinets}
\end{figure}

Note that, since firing is atomic but not isolated,
the above is an implementation of \emph{true concurrent} Petri nets, which is usually more difficult to achieve than interleaving semantics.

For instance, consider the Petri net below:
\\\\
\noindent
\begin{BVerbatim}[tabsize=3, xleftmargin=1ex, gobble=2]
        main = do
            p1 <- atomic (newPlace 1)
            p2 <- atomic (newPlace 0)
            p3 <- atomic (newPlace 0)
            p4 <- atomic (newPlace 0)
            transition [p1] [p3, p4]
            transition [p1, p2] [p4]
\end{BVerbatim}
\hfill
\begin{minipage}[t]{.33\linewidth}
    \begin{tikzpicture}
        \node [place,tokens=1, label=left:$p_1$] (p1) at (0,2) {};
        \node [place, label=right:$p_2$] (p2) at (1,2) {};
        
        \node [place, label=left:$p_3$] (p3) at (0,0) {};
        \node [place, label=right:$p_4$] (p4) at (1,0) {};
        
        \node [transition,label=left:$t_1$] (t1) at (0,1) {}
            edge [pre]  (p1)
            edge [post] (p3)
            edge [post] (p4);
        
        \node [transition,label=right:$t_2$] (t2) at (1,1) {}
            edge [pre]  (p1)
            edge [pre]  (p2)
            edge [post] (p4);
    \end{tikzpicture}
\end{minipage}
\\[1ex]
Since $p_1$ has only one token either $t_1$ or $t_2$ fires. In fact,
whenever $t_2$ acquires the token it will fail to acquire the other
from $p_2$ and hence its transaction retries releasing the token.
